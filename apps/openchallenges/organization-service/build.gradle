buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.flywaydb:flyway-mysql:${flywaydbVersion}"
  }
}

plugins {
  id 'com.diffplug.spotless' version "${spotlessVersion}"
  id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
  id 'jacoco'
  id 'java'
  id 'org.flywaydb.flyway' version "${flywaydbVersion}"
  id 'org.sonarqube' version "${sonarqubeVersion}"
  id 'org.springframework.boot' version "${springBootVersion}"
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.12'
  implementation "com.fasterxml.jackson.core:jackson-databind:${fasterxmlVersion}"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${fasterxmlVersion}"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${fasterxmlVersion}"
  implementation "org.flywaydb:flyway-core:${flywaydbVersion}"
  implementation "org.flywaydb:flyway-mysql:${flywaydbVersion}"
  implementation "org.hibernate.search:hibernate-search-backend-elasticsearch:${hibernateSearchVersion}"
  implementation "org.hibernate.search:hibernate-search-mapper-orm:${hibernateSearchVersion}"
  implementation "org.keycloak:keycloak-admin-client:${keycloakVersion}"
  implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"
  implementation "org.sagebionetworks:util:${openchallengesVersion}"
  implementation "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
  implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${springCloudVersion}"
  implementation "org.springframework.cloud:spring-cloud-starter-openfeign:${springCloudVersion}"
  implementation "org.springframework.data:spring-data-commons:${springDataVersion}"
  runtimeOnly 'mysql:mysql-connector-java:8.0.30'
  testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
  testRuntimeOnly 'com.h2database:h2:2.1.214'

  // Enable distributed tracing
  implementation "org.springframework.cloud:spring-cloud-starter-sleuth:${springCloudVersion}"
  implementation "org.springframework.cloud:spring-cloud-sleuth-zipkin:${springCloudVersion}"

  implementation "org.springframework.cloud:spring-cloud-starter-config:${springCloudVersion}"
  implementation "org.sagebionetworks.openchallenges:openchallenges-app-config-data:${openchallengesVersion}"

  implementation 'io.micrometer:micrometer-registry-prometheus:1.10.4'
}

group = 'org.sagebionetworks.openchallenges'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

bootBuildImage {
  imageName = "ghcr.io/sage-bionetworks/${project.name}-base:local"
}

test {
	useJUnitPlatform()

  testLogging.showStandardStreams = true

  beforeTest { descriptor ->
     logger.lifecycle("Running test: " + descriptor)
  }

  failFast = true

  testLogging {
    events("passed", "skipped", "failed")
  }

  finalizedBy jacocoTestReport
}

jacocoTestReport {
  reports {
    xml.required = true
  }

  afterEvaluate {
    // A single star (*) selects files AND subdirectories.
    // Exclude Java classes from the JaCoCo report. These files must be excluded from SonarCloud
    // report separatedly (see SonarCloud config)
    def autoGeneratedFiles = [
      '**/api/*.*',
      '**/configuration/EnumConverterConfiguration*.*',
      '**/configuration/Flyway*.*',
      '**/configuration/HibernateSearch*.*',
      '**/configuration/HomeController*.*',
      '**/configuration/Keycloak*.*',
      '**/configuration/SpringDocConfiguration*.*',
      '**/model/dto/**',
      '**/RFC3339DateFormat.*'
    ]
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: autoGeneratedFiles
        )
    }))
  }
}

sonar {
  // A single star (*) only selects files, not subdirectories.
  def autoGeneratedFiles = [
    '**/api/*',
    '**/configuration/EnumConverterConfiguration*.*',
    '**/configuration/Flyway*.*',
    '**/configuration/HibernateSearch*.*',
    '**/configuration/HomeController*.*',
    '**/configuration/Keycloak*.*',
    '**/configuration/SpringDocConfiguration*.*',
    '**/model/dto/**',
    '**/RFC3339DateFormat.*'
  ]
  properties {
    property("sonar.projectKey", "openchallenges-organization-service")
    property("sonar.organization", "sage-bionetworks")
    property("sonar.host.url", "https://sonarcloud.io")
    // Include the pull request number (ignored if not set)
    property("sonar.pullrequest.key", System.getenv('SONAR_PULL_REQUEST_NUMBER'))
  }
}

spotless {
  // format 'misc', {
  //   target '*.gradle', '*.md', '.gitignore'

  //   trimTrailingWhitespace()
  //   indentWithSpaces(2)
  //   endWithNewline()
  // }

  java {
    target 'src/*/java/**/*.java'

    importOrder()
    removeUnusedImports()
    googleJavaFormat()
  }
}

// flyway {
//   url = 'jdbc:mysql://openchallenges-mariadb:3306/challenge'
//   user = 'maria'
//   password = 'changeme'
//   // cleanDisabled = false
//   // schemas = ['schema1', 'schema2', 'schema3']
//   // placeholders = [
//   //   'keyABC': 'valueXYZ',
//   //   'otherplaceholder': 'value123'
//   // ]
// }

