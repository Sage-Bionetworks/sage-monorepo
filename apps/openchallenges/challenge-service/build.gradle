buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.flywaydb:flyway-mysql:${flywaydbVersion}"
  }
}

plugins {
  id 'jacoco'
  id 'java'
  id 'org.sonarqube' version "${sonarqubeVersion}"
  id 'org.springframework.boot' version "${springBootVersion}"
  id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
  id "org.flywaydb.flyway" version "${flywaydbVersion}"
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  implementation "org.hibernate.search:hibernate-search-mapper-orm:${hibernateSearchVersion}"
  implementation "org.hibernate.search:hibernate-search-backend-elasticsearch:${hibernateSearchVersion}"

  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.12'
  implementation "com.fasterxml.jackson.core:jackson-databind:${fasterxmlVersion}"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${fasterxmlVersion}"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${fasterxmlVersion}"
  implementation "org.flywaydb:flyway-core:${flywaydbVersion}"
  implementation "org.flywaydb:flyway-mysql:${flywaydbVersion}"
  implementation "org.sagebionetworks:util:${openchallengesVersion}"

  implementation 'org.springframework.boot:spring-boot-starter-amqp:2.7.7'

  implementation "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
  // developmentOnly 'org.springframework.boot:spring-boot-devtools'

  implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
  implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${springCloudVersion}"
  implementation "org.springframework.data:spring-data-commons:${springDataVersion}"
  runtimeOnly 'mysql:mysql-connector-java:8.0.30'
  testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

  implementation "org.springframework.cloud:spring-cloud-starter-config:${springCloudVersion}"

  implementation "org.sagebionetworks.openchallenges:openchallenges-app-config-data:${openchallengesVersion}"
}

group = 'org.sagebionetworks.openchallenges'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

test {
  testLogging.showStandardStreams = true

  beforeTest { descriptor ->
     logger.lifecycle("Running test: " + descriptor)
  }

  failFast = true

  testLogging {
    events("passed", "skipped", "failed")
  }

  finalizedBy jacocoTestReport
}

jacocoTestReport {
  reports {
    xml.required = true
  }

    afterEvaluate {
    // A single star (*) selects files AND subdirectories.
    // Exclude Java classes from the JaCoCo report. These files must be excluded from SonarCloud
    // report separatedly (see SonarCloud config)
    def autoGeneratedFiles = [
      '**/api/*.*',
      '**/configuration/EnumConverterConfiguration*.*',
      '**/configuration/HomeController*.*',
      '**/configuration/SpringDocConfiguration*.*',
      '**/model/dto/**',
      '**/RFC3339DateFormat.*'
    ]
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: autoGeneratedFiles
        )
    }))
  }
}

sonar {
    // A single star (*) only selects files, not subdirectories.
  def autoGeneratedFiles = [
    '**/api/*',
    '**/configuration/EnumConverterConfiguration*.*',
    '**/configuration/HomeController*.*',
    '**/configuration/SpringDocConfiguration*.*',
    '**/model/dto/**',
    '**/RFC3339DateFormat.*'
  ]
  properties {
    property("sonar.host.url", "https://sonarcloud.io")
    property("sonar.organization", "sage-bionetworks")
    property("sonar.projectKey", "${project.name}")
    // We still scan the auto-generated files to be aware of the bugs and code smells they include.
    // property("sonar.exclusions", autoGeneratedFiles.join(","))
    property("sonar.coverage.exclusions", autoGeneratedFiles.join(","))
    // Include the pull request number (ignored if not set)
    property("sonar.pullrequest.key", System.getenv('SONAR_PULL_REQUEST_NUMBER'))
  }
}

bootBuildImage {
  imageName = "ghcr.io/sage-bionetworks/${project.name}-base:local"
}

// flyway {
//   url = 'jdbc:mysql://openchallenges-mariadb:3306/challenge'
//   user = 'maria'
//   password = 'changeme'
//   // cleanDisabled = false
//   // schemas = ['schema1', 'schema2', 'schema3']
//   // placeholders = [
//   //   'keyABC': 'valueXYZ',
//   //   'otherplaceholder': 'value123'
//   // ]
// }

