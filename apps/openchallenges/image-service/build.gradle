buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'com.diffplug.spotless' version "${spotlessVersion}"
  id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
  id 'java'
  id 'jvm-test-suite'
  id 'org.springframework.boot' version "${springBootVersion}"
}

repositories {
  mavenCentral()
  mavenLocal()
}

testing {
  suites {
    test {
      useJUnitJupiter()
    }

    integrationTest(JvmTestSuite) {
      dependencies {
        implementation project
      }
      sources {
        java {
          srcDirs = ['src/integrationTest/java']
        }
      }
      targets {
        all {
          testTask.configure {
            shouldRunAfter(test)
          }
        }
      }
    }
  }
}

dependencies {
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.12'
  implementation "com.fasterxml.jackson.core:jackson-databind:${fasterxmlVersion}"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${fasterxmlVersion}"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${fasterxmlVersion}"
  implementation "org.sagebionetworks:util:${openchallengesVersion}"
  implementation "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
  implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
  implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${springCloudVersion}"
  implementation "org.springframework.data:spring-data-commons:${springDataVersion}"
  testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
  testImplementation 'org.assertj:assertj-core:3.23.1'

  // Enable distributed tracing
  implementation "org.springframework.cloud:spring-cloud-starter-sleuth:${springCloudVersion}"
  implementation "org.springframework.cloud:spring-cloud-sleuth-zipkin:${springCloudVersion}"

  implementation "org.springframework.cloud:spring-cloud-starter-config:${springCloudVersion}"
  implementation "org.sagebionetworks.openchallenges:openchallenges-app-config-data:${openchallengesVersion}"

  implementation 'io.micrometer:micrometer-registry-prometheus:1.10.4'
  implementation 'com.squareup:pollexor:3.0.0'
}

group = 'org.sagebionetworks.openchallenges'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

bootBuildImage {
  imageName = "sagebionetworks/${project.name}-base:local"
}

test {
	useJUnitPlatform()

  testLogging.showStandardStreams = true

  beforeTest { descriptor ->
     logger.lifecycle("Running test: " + descriptor)
  }

  failFast = true

  testLogging {
    events("passed", "skipped", "failed")
  }
}

spotless {
  // format 'misc', {
  //   target '*.gradle', '*.md', '.gitignore'

  //   trimTrailingWhitespace()
  //   indentWithSpaces(2)
  //   endWithNewline()
  // }

  java {
    target 'src/*/java/**/*.java'

    importOrder()
    removeUnusedImports()
    googleJavaFormat()
  }
}
